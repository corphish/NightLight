def taskRequests = getGradle().getStartParameter().getTaskRequests().toString()
def isPlayStore = taskRequests.contains("generic") || taskRequests.contains("Generic") || taskRequests.contains("donate") || taskRequests.contains("Donate")

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

if (isPlayStore) {
    apply plugin: 'com.google.firebase.crashlytics'
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

android {
    compileSdkVersion 30

    def getGitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    defaultConfig {
        applicationId "com.corphish.nightlight"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 60
        versionName "3.0.0-pre-beta-07"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "GitHash", "\"${getGitHash()}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    lintOptions {
        abortOnError false
    }

    flavorDimensions "buildVariant"

    productFlavors {
        generic {
            applicationId "com.corphish.nightlight.generic"
            dimension "buildVariant"
        }
        donate {
            applicationId "com.corphish.nightlight.donate"
            dimension "buildVariant"
        }
        foss {
            applicationId "com.corphish.nightlight.generic"
            dimension "buildVariant"
        }
    }

    buildFeatures {
        viewBinding = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin core
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Android core
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation "androidx.preference:preference-ktx:1.1.1"

    // UI
    implementation 'com.google.android.material:material:1.3.0-alpha04'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.github.mc0239:EditableSeekBar:v1.3.0'
    implementation 'com.github.corphish:widgets-ktx:0.4.1'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'com.ernestoyaquello.stepperform:vertical-stepper-form:2.3.0'

    // Lifecycle
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // Root
    implementation 'eu.chainfire:libsuperuser:1.0.0.201704021214'

    // Utilities
    implementation 'com.luckycatlabs:SunriseSunsetCalculator:1.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'

    // Crash reporting
    genericImplementation 'com.google.firebase:firebase-crashlytics:17.3.0'
    donateImplementation 'com.google.firebase:firebase-crashlytics:17.3.0'
}
